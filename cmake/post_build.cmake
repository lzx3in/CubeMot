# Post-build artifact processing - consolidates outputs into target/ directory

function(get_target_output_dir OUTPUT_VAR)
    set(${OUTPUT_VAR} "${CMAKE_SOURCE_DIR}/target/${BOARD}/${CMAKE_BUILD_TYPE}" PARENT_SCOPE)
endfunction()

get_target_output_dir(TARGET_OUTPUT_DIR)

configure_file(
    ${CMAKE_SOURCE_DIR}/boards/${BOARD}/openocd.cfg
    ${TARGET_OUTPUT_DIR}/openocd.cfg
    COPYONLY
)

# Generate binary and hex files for flashing
add_custom_command(TARGET ${CMAKE_PROJECT_NAME} POST_BUILD
    COMMAND ${CMAKE_OBJCOPY} -O binary ${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}.elf ${TARGET_OUTPUT_DIR}/${CMAKE_PROJECT_NAME}.bin
    COMMAND ${CMAKE_OBJCOPY} -O ihex ${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}.elf ${TARGET_OUTPUT_DIR}/${CMAKE_PROJECT_NAME}.hex
    COMMENT "Generating binary and hex files"
)

# Generate disassembly listing
add_custom_command(TARGET ${CMAKE_PROJECT_NAME} POST_BUILD
    COMMAND ${CMAKE_OBJDUMP} -d ${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}.elf > ${TARGET_OUTPUT_DIR}/${CMAKE_PROJECT_NAME}.lst
    COMMENT "Generating disassembly listing"
)

# Generate memory usage report
add_custom_command(TARGET ${CMAKE_PROJECT_NAME} POST_BUILD
    COMMAND ${CMAKE_SIZE} -A -d ${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}.elf > ${TARGET_OUTPUT_DIR}/memory_usage.txt
    COMMAND ${CMAKE_SIZE} -B ${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}.elf >> ${TARGET_OUTPUT_DIR}/memory_usage.txt
    COMMENT "Generating memory usage report"
)

# Generate size report
add_custom_command(TARGET ${CMAKE_PROJECT_NAME} POST_BUILD
    COMMAND ${CMAKE_SIZE} -B ${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}.elf > ${TARGET_OUTPUT_DIR}/size_report.txt
    COMMENT "Generating size report"
)

# Copy map file (generated by linker in build/ to target/)
add_custom_command(TARGET ${CMAKE_PROJECT_NAME} POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy
        ${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}.map
        ${TARGET_OUTPUT_DIR}/${CMAKE_PROJECT_NAME}.map
    COMMENT "Copying map file from build/ to target/"
)

# Copy elf file from build/ to target/
add_custom_command(TARGET ${CMAKE_PROJECT_NAME} POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy
        ${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}.elf
        ${TARGET_OUTPUT_DIR}/${CMAKE_PROJECT_NAME}.elf
    COMMENT "Copying elf file from build/ to target/"
)

# Clean only target/ directory files (build/ directory not affected)
set_target_properties(${CMAKE_PROJECT_NAME} PROPERTIES
    ADDITIONAL_CLEAN_FILES "
        ${TARGET_OUTPUT_DIR}/${CMAKE_PROJECT_NAME}.elf;
        ${TARGET_OUTPUT_DIR}/${CMAKE_PROJECT_NAME}.map;
        ${TARGET_OUTPUT_DIR}/${CMAKE_PROJECT_NAME}.bin;
        ${TARGET_OUTPUT_DIR}/${CMAKE_PROJECT_NAME}.hex;
        ${TARGET_OUTPUT_DIR}/${CMAKE_PROJECT_NAME}.lst;
        ${TARGET_OUTPUT_DIR}/size_report.txt;
        ${TARGET_OUTPUT_DIR}/memory_usage.txt
    "
)
