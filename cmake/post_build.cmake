# Post-build artifact processing function
# Call this function in the same directory where the target is created

function(get_target_output_dir OUTPUT_VAR)
    set(${OUTPUT_VAR} "${CMAKE_SOURCE_DIR}/target/${BOARD}/${CMAKE_BUILD_TYPE}" PARENT_SCOPE)
endfunction()

function(consolidate_outputs_to_target_dir TARGET_NAME)
    get_target_output_dir(TARGET_OUTPUT_DIR)

    # Use generator expression to get the actual output path of the target
    set(ELF_FILE "$<TARGET_FILE:${TARGET_NAME}>")
    # Map file is generated by linker in CMAKE_BINARY_DIR (not in target's dir)
    set(MAP_FILE "${CMAKE_BINARY_DIR}/${TARGET_NAME}.map")

    configure_file(
        ${CMAKE_SOURCE_DIR}/boards/${BOARD}/openocd.cfg
        ${TARGET_OUTPUT_DIR}/openocd.cfg
        COPYONLY
    )

    # Generate binary and hex files for flashing
    add_custom_command(TARGET ${TARGET_NAME} POST_BUILD
        COMMAND ${CMAKE_OBJCOPY} -O binary ${ELF_FILE} ${TARGET_OUTPUT_DIR}/${TARGET_NAME}.bin
        COMMAND ${CMAKE_OBJCOPY} -O ihex ${ELF_FILE} ${TARGET_OUTPUT_DIR}/${TARGET_NAME}.hex
        COMMENT "Generating binary and hex files"
    )

    # Generate disassembly listing
    add_custom_command(TARGET ${TARGET_NAME} POST_BUILD
        COMMAND ${CMAKE_OBJDUMP} -d ${ELF_FILE} > ${TARGET_OUTPUT_DIR}/${TARGET_NAME}.lst
        COMMENT "Generating disassembly listing"
    )

    # Generate memory usage report
    add_custom_command(TARGET ${TARGET_NAME} POST_BUILD
        COMMAND ${CMAKE_SIZE} -A -d ${ELF_FILE} > ${TARGET_OUTPUT_DIR}/memory_usage.txt
        COMMAND ${CMAKE_SIZE} -B ${ELF_FILE} >> ${TARGET_OUTPUT_DIR}/memory_usage.txt
        COMMENT "Generating memory usage report"
    )

    # Generate size report
    add_custom_command(TARGET ${TARGET_NAME} POST_BUILD
        COMMAND ${CMAKE_SIZE} -B ${ELF_FILE} > ${TARGET_OUTPUT_DIR}/size_report.txt
        COMMENT "Generating size report"
    )

    # Copy map file (generated by linker in build/ to target/)
    add_custom_command(TARGET ${TARGET_NAME} POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy
            ${MAP_FILE}
            ${TARGET_OUTPUT_DIR}/${TARGET_NAME}.map
        COMMENT "Copying map file from build/ to target/"
    )

    # Copy elf file from build/ to target/
    add_custom_command(TARGET ${TARGET_NAME} POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy
            ${ELF_FILE}
            ${TARGET_OUTPUT_DIR}/${TARGET_NAME}.elf
        COMMENT "Copying elf file from build/ to target/"
    )

    # Clean only target/ directory files (build/ directory not affected)
    set_target_properties(${TARGET_NAME} PROPERTIES
        ADDITIONAL_CLEAN_FILES "
            ${TARGET_OUTPUT_DIR}/${TARGET_NAME}.elf;
            ${TARGET_OUTPUT_DIR}/${TARGET_NAME}.map;
            ${TARGET_OUTPUT_DIR}/${TARGET_NAME}.bin;
            ${TARGET_OUTPUT_DIR}/${TARGET_NAME}.hex;
            ${TARGET_OUTPUT_DIR}/${TARGET_NAME}.lst;
            ${TARGET_OUTPUT_DIR}/size_report.txt;
            ${TARGET_OUTPUT_DIR}/memory_usage.txt
        "
    )
endfunction()
